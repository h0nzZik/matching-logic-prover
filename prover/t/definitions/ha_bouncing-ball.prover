// Kore axiomatization of bouncing ball HA

sort State

symbol reach( State , State ) : Bool

axiom functional(reach)

symbol evolve( State , Real ) : State

symbol valZ( State ) : Real
symbol valV( State ) : Real

axiom functional(valZ)
axiom functional(valV)

symbol mode( State ) : Int

axiom functional(mode)
axiom functional(evolve)

symbol discreteTransitionFrom( State ) : State
axiom functional( discreteTransitionFrom )

symbol jumpCondition( State , State ) : Bool
axiom functional( jumpCondition )

axiom smt-lemma( \forall { S1 { State } , S2 { State } }
                    \equals( jumpCondition( S1 { State }, S2 { State })
                           , \and( \equals( mode( S1 { State }), mode( S2 { State } ))
                                 , \equals( valZ( S1 { State }), 0.0 )
                                 , \equals( valZ( S2 { State }), 0.0 )
                                 , \equals( valV( S2 { State })
                                          , minus( 0.0, valV( S1 { State }))
                                          )
                                 )
                           )
                )



axiom smt-lemma( \forall { S1 { State } , S2 { State } }
                    \and( \implies( \equals( S2 { State } , discreteTransitionFrom( S1 { State }))
                                  , jumpCondition( S1 { State } , S2 { State })
                                  )
                        , \implies( jumpCondition( S1 { State } , S2 { State })
                                  , \equals( S2 { State } , discreteTransitionFrom( S1 { State }))
                                  )
                        )
               )

symbol stateSatisfiesModeInvariant( State ) : Bool

axiom functional(stateSatisfiesModeInvariant)


axiom smt-lemma( \forall { S1 { State } }
                     \equals( stateSatisfiesModeInvariant(S1 { State })
                            , \not(lt( valZ(S1 { State }), 0.0 ))
                            )
               )

symbol acc ( Real ) : Real

axiom functional(acc)

axiom smt-lemma( \forall { R { Real } }
                    \equals( acc( R { Real } ) , minus(0.0, R { Real } )))

axiom smt-lemma( \forall { S1 { State } , S2 { State } , T { Real } }
                    \and( \implies( \and( gt ( T { Real }, 0.0 )
                                            , \equals( S2 { State }
                                                     , evolve( S1 { State }, T { Real })
                                                     )
                                            )
                                      , \and( \equals( mode(S2 { State }) , mode(S1 { State }))
                                            , \equals( valV(S2 { State })
                                                     , minus(valV(S1 { State }), mult(10.0, T { Real }))
                                                     )
                                            , \equals( valZ(S2 { State })
                                                     , minus( plus( valZ( S1 { State })
                                                                  , mult(valV( S1 { State }), T { Real })
                                                                  )
                                                            , div(mult(10.0, mult(T { Real }, T { Real })), 2.0)
                                                            )
                                                     )
                                            )
                                      )
                              , \implies ( \and( \equals( mode(S2 { State }) , mode(S1 { State }))
                                               , \equals( valV(S2 { State })
                                                        , minus(valV(S1 { State }), mult(10.0, T { Real }))
                                                        )
                                               , \equals( valZ(S2 { State })
                                                        , minus( plus( valZ( S1 { State })
                                                                     , mult(valV( S1 { State }), T { Real })
                                                                     )
                                                               , div(mult(10.0, mult(T { Real }, T { Real })), 2.0)
                                                               )
                                                        )
                                               )
                                         , \equals( S2 { State } , evolve( S1 { State } , T { Real } ) )
                                         )
                          )
                  )


symbol intervalSatisfiesModeInvariant( State , Real ) : Bool

axiom functional(intervalSatisfiesModeInvariant)

axiom smt-lemma( \forall { S { State } , T { Real } }
                \and( \implies( \and( intervalSatisfiesModeInvariant( S { State }, T { Real } )
                                    , gt( T { Real }, 0.0 )
                                    )
                              , \and( stateSatisfiesModeInvariant( S { State } )
                                    , \forall { T2 { Real } }
                                          \implies( \and( gt( T2 { Real }, 0.0 )
                                                        , lt( T2 { Real }, T { Real })
                                                        )
                                                  , stateSatisfiesModeInvariant(evolve( S { State }, T2 { Real }))
                                                  )
                                    )
                              )
                    , \implies( \and ( gt( T { Real } , 0.0 )
                                     , stateSatisfiesModeInvariant( S { State } )
                                     , \forall { T2 { Real } }
                                           \implies( \and( gt( T2 { Real }, 0.0 )
                                                         , lt( T2 { Real }, T { Real })
                                                         )
                                                   , stateSatisfiesModeInvariant(evolve( S { State }, T2 { Real }))
                                                   )
                                     )
                              , intervalSatisfiesModeInvariant( S { State } , T { Real })
                              )
                    )
                )


axiom \forall { S1 { State },  S2 { State } }
      \iff-lfp( reach( S1 { State } , S2 { State } )
              , \or ( \exists { } \and ( \equals( S1 { State }, S2 { State })
                                       , \equals( mode(S1 { State }), mode(S2 { State }))
                                       , \equals( valV(S1 { State }), valV(S2 { State }))
                                       , \equals( valZ(S1 { State }), valZ(S2 { State }))
                                       )
                    , \exists { T { Real } } \and( gt( T { Real }, 0.0 )
                                                 , \equals( mode ( S1 { State }), mode( S2 { State } ))
                                                 , \equals( S2 { State }, evolve( S1 { State }, T { Real }))
                                                 , stateSatisfiesModeInvariant( S1 { State } )
                                                 , intervalSatisfiesModeInvariant( S1 { State }, T { Real })
                                                 , stateSatisfiesModeInvariant( S2 { State } )
                                                 )
                    , \exists { } \and( stateSatisfiesModeInvariant(S1 { State })
                                      , \equals( S2 { State }, discreteTransitionFrom( S1 {State })))
                    , \exists { SI { State } }  \and ( reach(S1 { State }, SI { State })
                                                     , reach( SI { State }, S2 { State })
                                                     )
                    )
              )
