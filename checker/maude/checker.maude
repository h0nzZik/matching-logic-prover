fmod CHECKER is protecting NAT + STRING .

---------------------------------------
---- Proof object definition
---------------------------------------

sort Id .
op `(_`) : Nat -> Id .

sort Proof .
op ___; : Id Digest Rule -> Proof [prec 40] .
op .Proof : -> Proof .
op __ : Proof Proof -> Proof [assoc id: .Proof prec 45 format(d n d)] .
---- proof with comments
op ___;_ : Id Digest Rule String -> Proof [prec 40] .
eq ID:Id D:Digest R:Rule ; S:String = ID:Id D:Digest R:Rule ; .

---- AtomicPattern contains element/set variables, symbols, and \bottom.

sorts AtomicPattern Digest .
subsort AtomicPattern Id < Digest .

op `(\evar`) : -> AtomicPattern .
op `(\svar`) : -> AtomicPattern .
op `(\symb`) : -> AtomicPattern .
op `(\bot`) : -> AtomicPattern .
op `(\app__`) : Id Id -> Digest .
op `(\imp__`) : Id Id -> Digest .
op `(\ex__`) : Id Id -> Digest . 
op `(\mu__`) : Id Id -> Digest . 


---- metalevel property

---- (variable) not occur free in (pattern)
op `(#nf__`) : Id Id -> Digest .

---- Rule annotations
---- Rules are divided into the following two categories:
----   1. (WF), which proves that a pattern is wellformed.
----   2. Other rules, which prove that a pattern is provable.
---- Note that we unify wellformedness and provability. The former is a meta-level
---- property while the latter is an object level property. 

sort Rule .

---- Wellformedness

op `(WF`) : -> Rule . 

---- Substitution related

op `(NotOccurFreeBasic`) : -> Rule .
op `(NotOccurFreeApp__`) : Id Id -> Rule .
op `(NotOccurFreeImp__`) : Id Id -> Rule .
op `(NotOccurFreeExBinding`) : -> Rule .
op `(NotOccurFreeExBody_`) : Id -> Rule .
op `(NotOccurFreeMuBinding`) : -> Rule .
op `(NotOccurFreeMuBody_`) : Id -> Rule .

---- Provability

---- Propositional rules
op `(PropK__`) : Id Id -> Rule . ---- p -> (q -> p)
op `(PropS___`) : Id Id Id -> Rule . ---- (p -> (q -> r)) -> (p -> q) -> (p -> r)
op `(PropF_`) : Id -> Rule . ---- (((p -> \bot) -> \bot) -> p)
op `(MP__`) : Id Id -> Rule . ---- p and p -> q deduces q

---- FOL rules
op `(ExistQ__`) : Id Id -> Rule . ---- p[y/x] -> \ex x . p, parameter order: (\ex x . p) y
op `(ExistR___`) : Id Id Id -> Rule . ---- p -> q deduces (\ex x . p) -> q, order: x p q

---- Propagation rules
op `(PropagationBotL_`) : Id -> Rule . ---- \bot p -> \bot 
op `(PropagationBotR_`) : Id -> Rule . ---- p \bot -> \bot
op `(PropagationOrL___`) : Id Id Id -> Rule . ---- (p1 \/ p2) q -> p1 q \/ p2 q, order: p1 p2 q
op `(PropagationOrR___`) : Id Id Id -> Rule . ---- q (p1 \/ p2) -> q p1 \/ q p2, order: p1 p2 q 
op `(PropagationExL___`) : Id Id Id -> Rule . ---- (\ex x . p) q -> \ex x . (p q), order: x p q
op `(PropagationExR___`) : Id Id Id -> Rule . ---- q (\ex x . p) -> \ex x . (q p), order: x p q
op `(FrameL_`) : Id -> Rule . ---- p -> q deduces p r -> q r
op `(FrameR_`) : Id -> Rule . ---- p -> q deduces r p -> r q


---- Fixpoint rules
op `(SVarSubst___`) : Id Id Id -> Rule . ---- p deduces p[q/X], order &p, X, q
op `(Prefixpoint__`) : Id Id -> Rule . ---- p[(\mu X . p) / X] -> \mu X . p, order X p
op `(KT_`) : Id -> Rule . ---- p[r/X] -> q deduces (\mu X . p) -> q, order &premis

---- Misc rules
op `(Existence`) : -> Rule . ---- \ex x . x
op `(Singleton`) : -> Rule . ---- \not (C1[x /\ p] /\ C2[x /\ \not p]) !!!


---------------------------------------
---- Proof checking definition
---------------------------------------

vars Phi Psi P Q R : Digest .
vars Pi Pi1 Pi2 Pi3 : Proof .
vars Rl Rl1 Rl2 : Rule .
vars M M1 M2 N N1 N2 M21 M22 N' : Nat .

---------------------------------------
---- Proof check:
----   PC1: check ids are (1) (2) (3) ...
----   PC2: check each proof step is correct
---------------------------------------

op check : Proof -> Bool .
op check-id : Proof -> Bool .
op check-step : Proof -> Bool .
eq check(Pi) = check-id(Pi) and check-step(Pi) .

---------------------------------------
---- PC1: check ids
---------------------------------------

op check-id-aux : Proof NzNat -> Bool .
op check-id0 : Proof -> Bool .
eq check-id(Pi) = check-id-aux(Pi, 0) .
eq check-id-aux(.Proof, N) = N >= 1 .
eq check-id-aux(((0) (\bot) (WF) ; Pi), 0) = check-id-aux(Pi, 1) .
eq check-id-aux(((N:NzNat) Phi Rl ; Pi), N:NzNat) = check-id-aux(Pi, N:NzNat + 1) .
eq check-id-aux(Pi, N) = false [owise] .


---------------------------------------
---- Some auxilliary definitions
---------------------------------------

---- Pattern lookup
op _[_] : Proof NzNat -> [Digest] .
eq (Pi1 ((N) Phi Rl ;) Pi2) [N] = Phi .

---- Auxilliary data structures: ExDigest,
---- which extends Digest by allowing *nested* constructs.
---- This is useful for proof checking.

sort ExDigest . subsort Digest < ExDigest .

op `(\app__`) : ExDigest ExDigest -> ExDigest .
op `(\imp__`) : ExDigest ExDigest -> ExDigest .
op `(\ex__`) : ExDigest ExDigest -> ExDigest . 
op `(\mu__`) : ExDigest ExDigest -> ExDigest . 

vars EP EQ EP1 EP2 EQ1 EQ2 : ExDigest .

---- Expand ExDigest to a full pattern.
---- op expand : Proof NzNat -> [Pattern] .
---- eq expand( (Pi1 ((N) AP:AtomicPattern Rl ;) Pi2), N)
----  = AP:AtomicPattern .
---- eq expand( (Pi1 ((N) (M) Rl ;) Pi2), N)
----  = expand(Pi1, M) .
---- eq expand( (Pi1 ((N) (\app (M1) (M2)) Rl ;) Pi2), N)
----  = (\app (expand(Pi1, M1)) (expand(Pi1, M2))) .
---- eq expand( (Pi1 ((N) (\imp (M1) (M2)) Rl ;) Pi2), N)
----  = (\imp (expand(Pi1, M1)) (expand(Pi1, M2))) .
---- eq expand( (Pi1 ((N) (\ex (M1) (M2)) Rl ;) Pi2), N)
----  = (\ex (expand(Pi1, M1)) (expand(Pi1, M2))) .
---- eq expand( (Pi1 ((N) (\mu (M1) (M2)) Rl ;) Pi2), N)
----  = (\mu (expand(Pi1, M1)) (expand(Pi1, M2))) .

---- Check if two ExDigest are equal *without* fully expanding them.
op eq? : Proof ExDigest ExDigest -> Bool .
eq eq?(Pi, EP, EP) = true .
ceq eq?(Pi, (M), (N)) = eq?(Pi, (N), (M)) if M > N .
eq eq?(Pi, (M), (N)) = eq?(Pi, (M), Pi[N]) .
eq eq?(Pi, P:AtomicPattern, P:AtomicPattern) = true .
eq eq?(Pi, P:AtomicPattern, Q:AtomicPattern) = false [owise] .
eq eq?(Pi, EP, (M)) = eq?(Pi, (M), EP) .
eq eq?(Pi, (M), EP) = eq?(Pi, Pi[M], EP) .
eq eq?(Pi, (\app EP1 EP2), (\app EQ1 EQ2)) = eq?(Pi, EP1, EQ1) and eq?(Pi, EP2, EQ2) .
eq eq?(Pi, (\imp EP1 EP2), (\imp EQ1 EQ2)) = eq?(Pi, EP1, EQ1) and eq?(Pi, EP2, EQ2) .
eq eq?(Pi, (\ex EP1 EP2), (\ex EQ1 EQ2)) = eq?(Pi, EP1, EQ1) and eq?(Pi, EP2, EQ2) .
eq eq?(Pi, (\mu EP1 EP2), (\mu EQ1 EQ2)) = eq?(Pi, EP1, EQ1) and eq?(Pi, EP2, EQ2) .
eq eq?(Pi, EP, EQ) = false [owise] .


---------------------------------------
---- PC2: check steps
---------------------------------------

---- Put a marker that separates the part that is checked from the rest. 

op _check-step-marker_ : Proof Proof -> Bool [format(g bn nr o)] .
eq check-step(Pi) = .Proof check-step-marker Pi .
eq Pi check-step-marker .Proof = true .

---- Proceed the proof step by step.

ceq Pi1 check-step-marker (((N) Phi Rl ;) Pi2)
  = (Pi1 ((N) Phi Rl ;)) check-step-marker Pi2
 if Pi1 |- ((N) Phi Rl ;) .
op _|-_ : Proof Proof -> Bool [prec 46] .


---------------------------------------
---- Some auxiliary checking functions.
---- Eliminate them in the future.
---------------------------------------

---- Check if a pattern is proved to be provable (not just wellformed).
op _proved-in_ : Id Proof -> Bool [prec 50] .
eq (N) proved-in Pi1 ((N) (M) Rl ;) Pi2 = Rl =/= (WF) .
eq (N) proved-in Pi = false [owise] .

---- Check if a pattern is an element variable.
op _is-evar-in_ : Id Proof -> Bool [prec 50] .
eq (N) is-evar-in Pi1 ((N) (\evar) (WF) ;) Pi2 = true .
eq (N) is-evar-in Pi = false [owise] .

---- Check if a pattern is a set variable.
op _is-svar-in_ : Id Proof -> Bool [prec 50] .
eq (N) is-svar-in Pi1 ((N) (\svar) (WF) ;) Pi2 = true .
eq (N) is-svar-in Pi = false [owise] .

---- Check if a pattern is a variable.
op _is-var-in_ : Id Proof -> Bool [prec 50] .
eq (N) is-var-in Pi = (N) is-evar-in Pi or (N) is-svar-in Pi .

---------------------------------------
---- Well-formedness checking
---------------------------------------

eq Pi |- ((0) (\bot) (WF) ;) = true .
eq Pi |- ((N:NzNat) (\bot) (WF) ;) = false .
eq Pi |- ((N) (\evar) (WF) ;) = true .
eq Pi |- ((N) (\svar) (WF) ;) = true .
eq Pi |- ((N) (\symb) (WF) ;) = true .
eq Pi |- ((N) (\imp (M1) (M2)) (WF) ;) = M1 < N and M2 < N .
eq Pi |- ((N) (\ex (M1) (M2)) (WF) ;)
 = M1 < N and M2 < N and (M1) is-evar-in Pi .

---------------------------------------
---- Substitution related checking
---------------------------------------

---- (Substitution phi psi x)

---- (NotOccurFree x p) holds if
----   x doesn't occur free in p
---- where x is an element/set variable
---- and p is a pattern

ceq Pi |- (N) (#nf (M1) (M2)) Rl ;
  = false
 if M1 >= N or M2 >= N or not (M1) is-var-in Pi .

ceq Pi |- (N) (#nf (M1) (M2)) (NotOccurFreeBasic) ;
  = true
 if (M2) is-var-in Pi and M1 =/= M2 .

eq Pi |- (N) (#nf (M1) (0)) (NotOccurFreeBasic) ;
 = true .

eq Pi1 ((M2) (\symb) (WF) ;) Pi2 |- (N) (#nf (M1) (M2)) (NotOccurFreeBasic) ;
 = true .

ceq Pi |- (N) (#nf (M1) (M2)) (NotOccurFreeImp (N1) (N2)) ;
 = true 
 if (#nf (M1) (M21)) := Pi[N1]
 /\ (#nf (M1) (M22)) := Pi[N2]
 /\ eq?(Pi, (M2), (\imp (M21) (M22))) .

ceq Pi |- (N) (#nf (M1) (M2)) (NotOccurFreeApp (N1) (N2)) ;
 = true 
 if (#nf (M1) (M21)) := Pi[N1]
 /\ (#nf (M1) (M22)) := Pi[N2]
 /\ eq?(Pi, (M2), (\app (M21) (M22))) .

ceq Pi |- (N) (#nf (M1) (M2)) (NotOccurFreeExBinding) ;
  = true
 if (\ex (M1) (M)) := Pi[M2] .

ceq Pi |- (N) (#nf (M1) (M2)) (NotOccurFreeExBody (N')) ;
  = true
 if (#nf (M1) (N2)) := Pi[N']
 /\ (\ex (M21) (N2)) := Pi[M2] .

ceq Pi |- (N) (#nf (M1) (M2)) (NotOccurFreeMuBinding) ;
  = true
 if (\mu (M1) (M)) := Pi[M2] .

ceq Pi |- (N) (#nf (M1) (M2)) (NotOccurFreeMuBody (N')) ;
  = true
 if (#nf (M1) (N2)) := Pi[N']
 /\ (\mu (M21) (N2)) := Pi[M2] .

---------------------------------------
---- Some auxiliary checking functions.
---- Eliminate them in the future.
---------------------------------------

---- PropK
eq Pi |- (N) (M) (PropK P Q) ;
 = eq?(Pi, (M), (\imp P (\imp Q P))) .

---- PropS 
eq Pi |- (N) (M) (PropS P Q R) ;
 = eq?(Pi, (M), (\imp (\imp P (\imp Q R)) (\imp (\imp P Q) (\imp P R)))) .

---- MP: Modus Ponens
eq Pi |- (N) (M) (MP P Q) ;
 = P proved-in Pi
   and Q proved-in Pi
   and eq?(Pi, Q, (\imp P (M))) .

---- Checking FOL rules

---- ExistsQ

endfm

